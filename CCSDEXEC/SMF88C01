/* Rexx */
/**********************************************************/
/*                                                        */
/* Function : This rexx scans SMF datset for smf88 records*/
/*            extracted from SMF (via IFASMFDP program)   */
/*                                                        */
/**********************************************************/
/*
   TRACE ?R
*/

"ispexec control errors return"

x      = msg('OFF')

arg t indsn

indsn  = "'"indsn"'"

smfid  = 88
numrec = 5000
outdsn = "'"userid()".SMF88.INPUT'"
outrep = "'"userid()".SMF88.REPORT'"

"FREE F(SORTIN)"
"FREE F(SORTOUT)"

if sysdsn(outdsn)='OK' then "DELETE "outdsn
if sysdsn(outrep)='OK' then "DELETE "outrep

if sysdsn(indsn) Â¬='OK'
then do
  say indsn " not cataloged"
  exit
end

say "SMF Data Set Analyzed: "indsn

opso   = "NEW CATALOG SPACE(5 1) CYL RECFM(v B) LRECL(4096)"
ops    = "NEW CATALOG SPACE(5 1) CYL RECFM(F B A) LRECL(132)"

"ALLOC F(sortin) DA("indsn") SHR"
"ALLOC F(sortout) DA("outdsn") "opso

"alloc f(sysin) recfm(f b) lrecl(80) new",
  "delete cylinders space(1,1) reuse dsorg(PS)"
"alloc f(sysout) recfm(v b) lrecl(255) new",
  "delete cylinders space(1,1) reuse dsorg(PS)"
"alloc f(sysprint) recfm(v b) lrecl(255) new",
  "delete cylinders space(1,1) reuse dsorg(PS)"

queue " OPTION VLSHRT "
queue " SORT  FIELDS=(133,8,BI,A,  "         /* +4 wgen VB  */
queue "               105,26,CH,A) "
queue " INCLUDE COND=(23,2,BI,EQ,X'0001',AND, "
queue "                6,1,BI,EQ,X'58')"
/*
queue "               105,length-of-cics,CH,EQ,C'cicssystem',AND, "
queue "               105,length-of-lsname,CH,EQ,C'lsname',AND, "
queue "               213,10,CH,EQ,C'*DASDONLY*')"
*/
nrec  = queued()

"execio " nrec " diskW sysin(fini"

address linkmvs "SORT"

"execio * diskR sysout(fini stem myLine."
"execio * diskR sysprint(fini stem myLine."

"free f(sortin sortout sysin sysout sysprint)"

do i  = 1 to myline.0
  say myline.i
  if pos("ICE054I",myline.i) > 0 | pos("CASO061I",myline.i) > 0
  then do
    if pos(" OUT: ",myline.i) > 0 | pos(",OUT ",myline.i)  > 0
    then do
      numin  = word(myline.i,6)
      numin  = strip(numin,'t',',')
      numout = word(myline.i,8)
    end
  end
  if pos("CASO061I",myline.i) > 0
  then do
    if pos(",OUT ",myline.i)  > 0
    then do
      numin  = substr(word(myline.i,4),1,8)
      numin  = strip(numin,'t',',')
      numin  = strip(numin,'l','0')
      numout = word(myline.i,5)
      numout = strip(numout,'l','0')
    end
  end
end

say "Number of records read: "numin
say "Number of records out: "numout

"ALLOC F(rport88) DA("outrep") "ops
"ALLOC F(smfdata) DA("outdsn") shr"

"ispexec vget (llqload smfhlq)"

"CALL '"SMFHLQ"."LLQLOAD"(IXGRPT1L)'"

if sysdsn(outdsn)='OK' then "DELETE "outdsn

call smf88r00 t outrep

if sysdsn(outrep)='OK' then "DELETE "outrep

x      = msg('ON')

exit
