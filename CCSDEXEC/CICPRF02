/*-------- REXX -------------------------------------------*/
/* Function   : select cics CSD programm information       */
/*---------------------------------------------------------*/
/*
   TRACE ?R
*/

arg kz table panel sel

"ISPEXEC CONTROL ERRORS RETURN"

CSEL = ""
"ISPEXEC VPUT CSEL"

START:
OP   = EQ
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(cicprf02)"

IF RC = 12 | RC = 20
THEN do
  CALL cic0000 cicprf02 cicprf02 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  EXIT
end

"ISPEXEC REMPOP"

select
  when zcmd = 1 THEN CSEL=PR
  when zcmd = 2 THEN CSEL=GR
  when zcmd = 3 THEN CSEL=SA
  when zcmd = 4 THEN CSEL=SD
  when zcmd = 5 THEN CSEL=UT
  when zcmd = 6 THEN CSEL=UC
  when zcmd = 7 THEN CSEL=NU
  when zcmd = 8 THEN CSEL=SO
  when zcmd = 9 THEN CSEL=PL
  otherwise
end

IF KZ = CSEL THEN EXIT

"ISPEXEC VPUT CSEL"

SELECT
  WHEN CSEL = "" THEN exit
  WHEN CSEL = PL THEN exit
  WHEN CSEL = PR
  then do
    CALL prf_selection
    fieldn  = "PROF"
    fieldv  = prf
  end
  WHEN CSEL = GR
  then do
    CALL group_selection
    fieldn  = "GROUP"
    fieldv  = group
  end
  WHEN CSEL = SA
  then do
    fieldn  = "SCRNSIZE"
    fieldv  = ALTERNATE
  end
  WHEN CSEL = SD
  then do
    fieldn  = "SCRNSIZE"
    fieldv  = DEFAULT
  end
  WHEN CSEL = UC
  then do
    fieldn  = "UCTRAN"
    fieldv  = yes
  end
  WHEN CSEL = UT
  then do
    fieldn  = "UCTRAN"
    fieldv  = TRANID
  end
  WHEN CSEL = NU
  then do
    fieldn  = "UCTRAN"
    fieldv  = NO
  end
  WHEN CSEL = SO
  then do
    fieldn = GROUP
    "ISPEXEC TBSORT " table " FIELDS("fieldn",C,A,PROF)"
    ausw = 'Profile sorted ' || fieldn
    call cicprf01 kz table ausw
    "ISPEXEC TBSORT " table " FIELDS(PROF)"
    signal start
  end
  otherwise exit
END

"ISPEXEC TBVCLEAR " table

tabname = cicprf||csel
field   = fieldn

call value field,fieldv

"ISPEXEC TBQUERY" table " NAMES(TNAMES)"
"ISPEXEC TBSARG " table " NEXT NAMECOND("field","op")"

"ISPEXEC TBERASE " tabname
"ISPEXEC TBCREATE " TABNAME " KEYS(PROF GROUP) NOWRITE NAMES"TNAMES

rc = 0
recfound = n

do while rc = 0
  "ISPEXEC TBSCAN " table
  if rc > 0 then leave
  "ISPEXEC TBADD " tabname " MULT(1000)"
  recfound = y
end

IF recfound = n
THEN do
  MESS1 = "no rows with searchargument found"
  MESS2 = "Profile"
  MESS3 = "searchargument " || fieldn " = " fieldv
  "ISPEXEC ADDPOP"
  "ISPEXEC DISPLAY PANEL(CIC0000)"
  "ISPEXEC REMPOP"
  "ISPEXEC TBEND" tabname
  signal start
end

"ISPEXEC TBTOP " tabname

ausw = "Profile " fieldn " = " fieldv

call cicprf01 kz tabname ausw

"ISPEXEC TBEND " tabname

signal start

EXIT

prf_selection:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(cicprf03)"

IF RC = 12 | RC = 20
THEN do
  CALL CIC0000 cicprf02 cicprf03 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  signal start
end

"ISPEXEC REMPOP"

IF POS('*',prf) = 1
then do
  "ispexec setmsg msg(cica001)"
  signal prf_selection
end
IF POS('*',prf) = 0
then do
  prf  = prf  || '*'
end

return

group_selection:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(cicprg04)"

IF RC = 12 | RC = 20
THEN do
  CALL CIC0000 cictrn02 cictrn04 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  signal start
end

"ISPEXEC REMPOP"

IF POS('*',group) = 1
then do
  "ispexec setmsg msg(cica001)"
  signal group_selection
end
IF POS('*',group) = 0
then do
  group = group || '*'
end

return
