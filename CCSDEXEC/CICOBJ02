/*-------- REXX -------------------------------------------*/
/* Function   : select cics CSD object information         */
/*---------------------------------------------------------*/
/*
   TRACE ?R
*/

arg kz table panel sel

"ISPEXEC CONTROL ERRORS RETURN"

CSEL = ""
"ISPEXEC VPUT CSEL"

DISPLAY:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(CICOBJ02)"

IF RC = 12 | RC = 20
THEN do
  CALL cic0000 cicobj02 cicobj02 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  EXIT
end

"ISPEXEC REMPOP"

select
  when zcmd = 1 THEN CSEL=OB
  when zcmd = 2 THEN CSEL=GR
  when zcmd = 3 THEN CSEL=TR
  when zcmd = 4 THEN CSEL=PR
  when zcmd = 5 THEN CSEL=TE
  when zcmd = 6 THEN CSEL=MA
  when zcmd = 7 THEN CSEL=CO
  when zcmd = 8 THEN CSEL=SE
  when zcmd = 9 THEN CSEL=TY
  when zcmd = 10 THEN CSEL=PF
  when zcmd = 11 THEN CSEL=FI
  when zcmd = 12 THEN CSEL=LS
  when zcmd = 13 THEN CSEL=SO
  when zcmd = 14 THEN CSEL=PL
  otherwise
end

IF KZ = CSEL THEN EXIT

"ISPEXEC VPUT CSEL"

SELECT
  WHEN CSEL = "" THEN exit
  WHEN CSEL = PL THEN exit
  WHEN CSEL = OB
  then do
    CALL object_selection
    fieldn  = "OBJ"
    fieldv  = obj
  end
  WHEN CSEL = GR
  then do
    CALL group_selection
    fieldn  = "GROUP"
    fieldv  = group
  end
  WHEN CSEL = TR
  then do
    CALL CICTRN00
    signal display
  end
  WHEN CSEL = PR
  then do
    CALL CICPRG00
    signal display
  end
  WHEN CSEL = TE
  then do
    CALL CICTRM00
    signal display
  end
  WHEN CSEL = MA
  then do
    CALL CICMAP00
    signal display
  end
  WHEN CSEL = CO
  then do
    CALL CICCON00
    signal display
  end
  WHEN CSEL = SE
  then do
    CALL CICSES00
    signal display
  end
  WHEN CSEL = TY
  then do
    CALL CICTYP00
    signal display
  end
  WHEN CSEL = PF
  then do
    CALL CICPRF00
    signal display
  end
  WHEN CSEL = FI
  then do
    CALL CICFIL00
    signal display
  end
  WHEN CSEL = LS
  then do
    CALL CICLSR00
    signal display
  end
  WHEN CSEL = SO
  then do
    CALL sort_selection
    if zcmd  = 1 then fieldn = 'OBJNAME'
                 else fieldn  = 'GROUP'
    "ISPEXEC TBSORT " table " FIELDS("fieldn",C,A,OBJ)"
    ausw = 'Object sorted ' || fieldn
    call cicobj01 kz table ausw
    "ISPEXEC TBSORT " table " FIELDS(OBJ,C,A,GROUP,C,A,OBJNAME)"
    signal display
  end
  otherwise exit
END

"ISPEXEC TBVCLEAR " table

tabname = cicobj||csel
field   = fieldn

call value field,fieldv

"ISPEXEC TBQUERY" table " NAMES(TNAMES)"
"ISPEXEC TBSARG " table " NEXT NAMECOND("field",EQ)"

"ISPEXEC TBERASE " tabname
"ISPEXEC TBCREATE " TABNAME " KEYS(OBJ OBJNAME GROUP) NOWRITE NAMES"TNAMES

rc = 0
recfound = n

do while rc = 0
  "ISPEXEC TBSCAN " table
  if rc > 0 then leave
  "ISPEXEC TBADD " tabname " MULT(1000)"
  recfound = y
end

IF recfound = n
THEN do
  MESS1 = "no rows with searchargument found"
  MESS2 = "searchargument " || fieldn || " = " || fieldv
  MESS3 = " "
  MESS4 = " "
  MESS5 = " "
  MESS6 = " "
  MESS7 = " "
  MESS8 = " "
  "ISPEXEC ADDPOP"
  "ISPEXEC DISPLAY PANEL(CIC0000)"
  "ISPEXEC REMPOP"
  "ISPEXEC TBEND" tabname
  signal display
end

"ISPEXEC TBTOP " tabname

ausw = "Object " fieldn " = " fieldv

call cicobj01 kz tabname ausw

"ISPEXEC TBEND " tabname

signal display

EXIT

object_selection:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(CICOBJ03)"

IF RC = 12 | RC = 20
THEN do
  CALL CIC0000 cicobj02 cicobj03 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  signal display
end

"ISPEXEC REMPOP"

IF POS('*',obj) = 1
then do
  "ispexec setmsg msg(cica001)"
  signal object_selection
end
IF POS('*',obj) = 0
then do
  obj = obj || '*'
end

return

group_selection:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(CICPRG04)"

IF RC = 12 | RC = 20
THEN do
  CALL CIC0000 cicobj02 cicprg04 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  signal display
end

"ISPEXEC REMPOP"

IF POS('*',group) = 1
then do
  "ispexec setmsg msg(cica001)"
  signal group_selection
end
IF POS('*',group) = 0
then do
  group = group || '*'
end

return

sort_selection:
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(CICOBJ04)"

IF RC = 12 | RC = 20
THEN do
  CALL CIC0000 cicobj02 cicobj04 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  signal display
end

"ISPEXEC REMPOP"

return
