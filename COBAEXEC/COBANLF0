/*-------- REXX -------------------------------------------*/
/* Function   : cobol information select program info      */
/*---------------------------------------------------------*/
/*
   TRACE ?R
*/

"ispexec control errors return"
parse source xx yy procname rest
signal on syntax  name failures
signal on failure name failures

arg           kz       ,
              table    ,
              table2   ,
              table3   ,
              panel    ,
              filtproc ,
              lineproc ,
              fromproc ,
              sel

"ispexec vget (csel f1 f2 f3)"

IF f1 = "" & f2 = "" & f3 = ""
then nop
else do
  v1   = EQ
  v2   = EQ
  v3   = EQ
  kz   = FS
  signal filter_done
end

START:
call fill_names
"ISPEXEC ADDPOP ROW(03) COLUMN(10)"
"ISPEXEC DISPLAY PANEL(COBAFI10)"

IF RC = 12 | RC = 20
THEN do
  CALL coba000 procname cobafi10 RC
  "ISPEXEC REMPOP"
  EXIT
end

IF ZCMD = TRACE
THEN DO
  zcmd = ' '
  trace ?r
end

IF RC = 8
THEN do
  "ISPEXEC REMPOP"
  EXIT
end

"ISPEXEC REMPOP"

if substr(zcmd,1,6) = filter
then do
  fi = length(zcmd)
  if fi > 6
  then do
    zparm =  substr(zcmd,8,fi-6)
    if zparm = 'CLEAR'
    then do
      "ispexec setmsg msg(coba006) msgloc(zcmd)"
      f1 = ""
      f2 = ""
      f3 = ""
      zparm  = ""
      "ispexec vput zparm"
      signal start
    end
  end
  signal start
end

IF f1 = "" & f2 = "" & f3 = ""
THEN do
  MESS1 = "no filter criteria specified"
  "ISPEXEC ADDPOP"
  "ISPEXEC DISPLAY PANEL(coba000)"
  "ISPEXEC REMPOP"
  signal start
end

"ispexec vput (f1 f2 f3)"

filter_done:

"ISPEXEC TBVCLEAR " table

fieldn = ""

if f1 > ""
then do
  field  = 'PGMNAME,'
  field  = field||v1
  fieldn = field
  pkomma = pos(',',field)
  name   = substr(field,1,pkomma-1)
  f1n    = name
  if pos('*',f1) = 0 then f1 = f1||'*'
  fieldv = f1
  call value name,fieldv
end

if f2 > ""
then do
  field  = 'COBREL,'
  field  = field||v2
  if fieldn ¬= "" then fieldn = fieldn||','
  fieldn = fieldn || field
  pkomma = pos(',',field)
  name   = substr(field,1,pkomma-1)
  f2n    = name
  if pos('*',f2) = 0 then f2 = f2||'*'
  fieldv = f2
  call value name,fieldv
end

if f3 > ""
then do
  field  = 'COBVERS,'
  field  = field||v3
  if fieldn ¬= "" then fieldn = fieldn||','
  fieldn = fieldn || field
  pkomma = pos(',',field)
  name   = substr(field,1,pkomma-1)
  f3n    = name
  if pos('*',f3) = 0 then f3 = f3||'*'
  fieldv = f3
  call value name,fieldv
end

tabname = 'R'||time('S')

"ISPEXEC TBQUERY" table " KEYS(TKEYS) KEYNUM(TKEYNUM) NAMES(TNAMES)"
"ISPEXEC TBSARG " table " NEXT NAMECOND("fieldn")"

"ISPEXEC TBERASE  " tabname
"ISPEXEC TBCREATE " TABNAME " KEYS"TKEYS "NOWRITE NAMES"TNAMES

rc = 0
recfound = n

do while rc = 0
  "ISPEXEC TBSCAN " table
  if rc > 0 then leave
  "ISPEXEC TBADD " tabname " MULT(1000)"
  recfound = y
end

IF recfound = n
THEN do
  MESS1 = "no rows with searchargument found"
  MESS2 = "searchargument "
  MESS3 = f1n " = " f1
  MESS4 = f2n " = " f2
  MESS5 = f3n " = " f3
  "ISPEXEC ADDPOP"
  "ISPEXEC DISPLAY PANEL(coba000)"
  "ISPEXEC REMPOP"
  "ISPEXEC TBEND" tabname
  signal start
end

"ISPEXEC TBTOP " tabname

call cobanldt ALL      ,
              tabname  ,
              table2   ,
              table3   ,
              panel    ,
              filtproc ,
              lineproc ,
              COBANLF0 ,
              sel

"ISPEXEC TBEND " tabname

"ispexec vget (zparm zcmd)"

if zcmd     = FILTER
then do
  if zparm  = 'CLEAR'
  then do
    f1      = ""
    f2      = ""
    f3      = ""
    zparm   = ""
    "ispexec  vput zparm"
  end
  signal      start
end

if kz       = FI then signal start

f1          = ""
f2          = ""
f3          = ""

"ispexec      vput (f1 f2 f3)"

exit

fill_names:

f1n    = 'Program'
f1K    = 'Name of program(s) to select'
f2n    = 'Cobol'
f2K    = 'Cobol version (COBOL/VS,COBOL/2)'
f3n    = 'Compiler'
f3K    = 'Compiler Version'

return

failures:
line = strip(sourceline(sigl))
Cond = condition('C') condition('D')
"ispexec vput (cond line)"
call cobasynt procname sigl errortext(rc) /* show what went wrong */
exit
