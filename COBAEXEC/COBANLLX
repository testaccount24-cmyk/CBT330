/*-------- REXX -------------------------------------------*/
/* Function   : select cobanal programs                    */
/* caller     : general table display procedure            */
/*---------------------------------------------------------*/
/*
    trace ?r
*/

"ispexec control errors return"
parse source xx yy procname rest
signal on syntax  name failures
signal on failure name failures

arg selection table table2 fromproc a

"ispexec vget tkey1"
ztkey1   = tkey1
tkey1    = 'PGMKEY'
"ispexec vput tkey1"

select
  when ztkey1 = 'OPTION'
  then do
    option    = selection
  end
  when ztkey1 = 'COBVERS'
  then do
    fn        = 'COBVERS'
    cobvers   = selection
  end
  when ztkey1 = 'STMT'
  then do
    fn        = 'STMT'
    stmt      = selection
  end
  when ztkey1 = 'LOADLIB'
  then do
    fn        = 'LOADLIB'
    loadlib   = selection
  end
  otherwise do
    MESS1 = "invalid option specified"
    MESS2 = "searchargument follows"
    MESS3 = 'TKEY1 = ' ztkey1
    MESS4 = 'Selection = ' selection
    "ISPEXEC ADDPOP"
    "ISPEXEC DISPLAY PANEL(COBA000)"
    "ISPEXEC REMPOP"
    signal finish
  end
end

"ispexec tbget " table

call value fn,selection

"ISPEXEC TBQUERY" table2 " KEYS(TKEY) KEYNUM(TKEYNUM) NAMES(TNAMES)"

tabname     = 'R'||time('S')

"ISPEXEC TBERASE " tabname

"ISPEXEC TBCREATE " TABNAME " KEYS"TKEY "NOWRITE NAMES"TNAMES

rc          = 0
recfound    = n

"ISPEXEC TBSARG " table2 " NEXT NAMECOND("fn")"

do while rc = 0
  "ISPEXEC TBSCAN " table2
  if rc     > 0 then leave
  "ISPEXEC TBADD " tabname " MULT(1000)"
  recfound  = y
end

IF recfound = n
THEN do
  MESS1 = "no rows with searchargument found"
  MESS2 = "searchargument follows"
  "ISPEXEC ADDPOP"
  "ISPEXEC DISPLAY PANEL(COBA000)"
  "ISPEXEC REMPOP"
  "ISPEXEC TBEND" tabname
  signal finish
end

"ISPEXEC TBTOP " tabname

Sel = 'programs with ' ztkey1 '=' selection

call cobanldt 'ALL'    ,
              tabname  ,
              table2   ,
              table3   ,
              COBANL00 ,
              COBANLF0 ,
              COBANLL0 ,
              COBANLLI ,
              sel

finish:
tkey1    = ztkey1
"ispexec vput tkey1"

exit

failures:
line = strip(sourceline(sigl))
Cond = condition('C') condition('D')
"ispexec vput (cond line)"
parse source x y procname z
call cobasynt procname sigl errortext(rc) /* show what went wrong */
exit
